HELP     TITLE 'QUEUE COMMAND - DISPLAY HELP'                     ONL02 HEL00010
******************************************************************ONL02 HEL00020
*                                                                 ONL02 HEL00030
* MODULE NAME = HELP                                              ONL02 HEL00040
*                                                                 ONL02 HEL00050
* DESCRIPTIVE NAME = THE QUEUE COMMAND HELP FACILITY DISPLAY      ONL02 HEL00060
*                                                                 ONL02 HEL00070
* AUTHOR = ROBERT M. JINKINS                                      ONL02 HEL00080
*          COMPUTER SCIENCES DIVISION                             ONL02 HEL00090
*          UNION CARBIDE CORP., NUCLEAR DIVISION                  ONL02 HEL00100
*          OAK RIDGE NATIONAL LABORATORY                          ONL02 HEL00110
*          P.O. BOX X                                             ONL02 HEL00120
*          OAK RIDGE, TENNESSEE  37830                            ONL02 HEL00130
*                                                                 ONL02 HEL00140
*          PHONE: (615) 574-7208/5300                             ONL02 HEL00150
*                  FTS  624-7208/5300                             ONL02 HEL00160
*                                                                 ONL02 HEL00170
*          SHARE INSTALLATION CODE = ONL                          ONL02 HEL00180
*                                                                 ONL02 HEL00190
* DATE WRITTEN = JUNE 83                                          ONL02 HEL00200
*                                                                 ONL02 HEL00210
* FUNCTION = TO DISPLAY THE HELP SCREENS IN RESPONSE TO A "HELP"  ONL02 HEL00220
*    SUB-COMMAND OR AN UNDEFINED/UNAUTHORIZED SUB-COMMAND VERB.   ONL02 HEL00230
*    HELP INFORMATION IS ONLY DISPLAYED FOR SUB-COMMANDS WHICH    ONL02 HEL00240
*    THE USER IS AUTHORIZED TO USE.                               ONL02 HEL00250
*                                                                 ONL02 HEL00260
*    THE #H MACRO IS USED TO DEFINE THE HELP SCREENS AND IS       ONL02 HEL00270
*    DISCUSSED IN THE COMMENTS AT THE BEGINNING OF THE #H MACRO.  ONL02 HEL00280
*    AN OPERAND OF THIS MACRO DEFINES THE AUTHORITY REQUIRED      ONL02 HEL00290
*    BEFORE A USER MAY SEE A GIVEN MESSAGE IN THE HELP DISPLAY.   ONL02 HEL00300
*    THE USER'S AUTHORITY FOR HELP DISPLAYS (QAHELP) IS SET IN    ONL02 HEL00310
*    THE AUTH CSECT BY DEFAULT TO THE USER'S AUTHORITY (QAFLAGS). ONL02 HEL00320
*                                                                 ONL06 HEL00330
* CHANGE ACTIVITY = NONE                                          ONL02 HEL00340
*                                                                 ONL02 HEL00350
******************************************************************ONL02 HEL00360
         EJECT ,                                                  ONL02 HEL00370
         MACRO ,                                                  ONL02 HEL00380
&NAME    #H    &TYPE,&OFFSET,&TEXT,&USER=0                        ONL02 HEL00390
.*****************************************************************ONL02 HEL00400
.*                                                                ONL02 HEL00410
.* MACRO NAME = #H                                                ONL02 HEL00420
.*                                                                ONL02 HEL00430
.* MODIFICATION ID = ONL02                                        ONL02 HEL00440
.*                                                                ONL02 HEL00450
.* DESCRIPTIVE NAME = QUEUE COMMAND HELP FACILITY MESSAGE ENTRY   ONL02 HEL00460
.*                                                                ONL02 HEL00470
.* AUTHOR = ROBERT M. JINKINS                                     ONL02 HEL00480
.*          OAK RIDGE NATIONAL LABORATORY                         ONL02 HEL00490
.*          4500-N MS-255                                         ONL02 HEL00500
.*          P.O. BOX X                                            ONL02 HEL00510
.*          OAK RIDGE, TENNESSEE  37830                           ONL02 HEL00520
.*                                                                ONL02 HEL00530
.*          PHONE: (615) 574-7208/5300                            ONL02 HEL00540
.*                  FTS  624-7208/5300                            ONL02 HEL00550
.*                                                                ONL02 HEL00560
.*          SHARE INSTALLATION CODE = ONL                         ONL02 HEL00570
.*                                                                ONL02 HEL00580
.* DATE WRITTEN = JUNE 83                                         ONL02 HEL00590
.*                                                                ONL02 HEL00600
.* FUNCTION = THIS MACRO IS USED TO GENERATE ENTRIES IN HELP      ONL02 HEL00610
.*    TABLE (IN THE HELP CSECT).  THE HELP TABLE ENTRIES, MAPPED  ONL02 HEL00620
.*    BY THE HTEDSECT, IS UESD TO GENERATE THE HELP SCREENS.      ONL02 HEL00630
.*                                                                ONL02 HEL00640
.* SYNTAX:  NAME  #H  TYPE,OFFSET,TEXT,USER=X                     ONL02 HEL00650
.*                                                                ONL02 HEL00660
.*   WHERE:                                                       ONL02 HEL00670
.*     NAME    - IS AN OPTIONL LABEL.                             ONL02 HEL00680
.*     TYPE    - THIS REQUIRED OPERAND IS CODED AS FOLLOWS:       ONL02 HEL00690
.*                 NORM  - MSG LINE WITH NO RESTRICTIONS.         ONL02 HEL00700
.*                 ACTV  - MSG LINE, QAHFACTV AUTHORITY REQUIRED. ONL02 HEL00710
.*                 BLOG  - MSG LINE, QAHFBLOG AUTHORITY REQUIRED. ONL02 HEL00720
.*                 SSSM  - MSG LINE, QAHFSSSM AUTHORITY REQUIRED. ONL02 HEL00730
.*                 SLOG  - MSG LINE, QAHFSLOG AUTHORITY REQUIRED. ONL02 HEL00740
.*                 EXEC  - MSG LINE, QAHFEXEC AUTHORITY REQUIRED. ONL02 HEL00750
.*                 OPER  - MSG LINE, QAHFOPER AUTHORITY REQUIRED. ONL02 HEL00760
.*                 SYSP  - MSG LINE, QAHFSYSP AUTHORITY REQUIRED. ONL02 HEL00770
.*                 QAHX... - AN EXPRESSION BEGINNING WITH THE     ONL02 HEL00780
.*                           CHARACTERS "QAHX" DEFINING THE       ONL02 HEL00790
.*                           REQUIRED AUTHORITY BITS              ONL02 HEL00800
.*                           (E.G. "QAHXSSSM+QAHXOPER").          ONL02 HEL00810
.*                 PAGE  - INDICATES END OF THE CURRENT SCREEN.   ONL02 HEL00820
.*                 END   - INDICATES END OF THE LAST SCREEN.      ONL02 HEL00830
.*                                                                ONL02 HEL00840
.*               MESSAGE LINES REQUIRING AUTHORITY WILL NOT BE    ONL02 HEL00850
.*               DISPLAYED UNLESS THE CORRESPONDING AUTHORITY     ONL02 HEL00860
.*               BITS ARE ON IN QAHELP.                           ONL02 HEL00870
.*                                                                ONL02 HEL00880
.*     OFFSET  - GIVES THE NUMBER OF SPACES FROM THE RIGHT SIDE   ONL02 HEL00890
.*               OF THE SCREEN TO THE BEGINNING OF THE MESSAGE    ONL02 HEL00900
.*               TEXT.  THE OFFSET MUST BE A SELF-DEFINING TERM   ONL02 HEL00910
.*               WITH A VALUE IN THE RANGE OF 0 TO 79.  THIS      ONL02 HEL00920
.*               OPERAND IS IGNORED FOR TYPES "PAGE" AND "END".   ONL02 HEL00930
.*                                                                ONL02 HEL00940
.*               DEFAULT:  0                                      ONL02 HEL00950
.*                                                                ONL02 HEL00960
.*     TEXT    - IS THE TEXT TO BE DISPLAYED ON THE MESSAGE LINE. ONL02 HEL00970
.*               THE TEXT MUST BE ENCLOSED IN APOSTROPHES.  THE   ONL02 HEL00980
.*               OFFSET PLUS THE ASSEMBLED LENGTH OF THE TEXT     ONL02 HEL00990
.*               MUST NOT EXCEED 80 CHARACTERS.  THIS OPERAND IS  ONL02 HEL01000
.*               IGNORED FOR TYPES "PAGE" AND "END".              ONL02 HEL01010
.*                                                                ONL02 HEL01020
.*               NO DEFAULT, MUST BE CODED IF TYPE IS NOT "PAGE"  ONL02 HEL01030
.*               OR "END"                                         ONL02 HEL01040
.*                                                                ONL02 HEL01050
.*     USER=X  - SPECIFIES OPTIONAL USER FLAGS FOR USE BY HELP    ONL02 HEL01060
.*               CSECT MODS MADE BY YOUR INSTALLATION.  STANDARD  ONL02 HEL01070
.*               HELP FACILITY PROCESSING DOES NOT USE THE USER   ONL02 HEL01080
.*               FLAGS.  THIS OPERAND IS IGNORED FOR TYPES "PAGE" ONL02 HEL01090
.*               AND "END".                                       ONL02 HEL01100
.*                                                                ONL02 HEL01110
.*               DEFAULT:  USER=0                                 ONL02 HEL01120
.*                                                                ONL02 HEL01130
.* CHANGE ACTIVITY = NONE                                         ONL02 HEL01140
.*                                                                ONL02 HEL01150
.*****************************************************************ONL02 HEL01160
         GBLA  &MSGCNT             NUM HELP MSG'S ON CURRENT PAGE ONL02 HEL01170
         LCLA  &OFF                OFFSET FOR MSG TEXT            ONL02 HEL01180
         LCLA  &L                  COMPUTED TEXT LENGTH           ONL02 HEL01190
         LCLA  &N                  CURRENT TEXT INDEX             ONL02 HEL01200
         LCLB  &P                  SPECIAL CHAR PAIR INDICATOR    ONL02 HEL01210
         LCLA  &TLEN               LENGTH OF OFFSET PLUS TEXT     ONL02 HEL01220
         AIF   ('&TYPE' EQ 'NORM').LINE                           ONL02 HEL01230
         AIF   ('&TYPE' EQ 'ACTV').LINE                           ONL02 HEL01240
         AIF   ('&TYPE' EQ 'BLOG').LINE                           ONL02 HEL01250
         AIF   ('&TYPE' EQ 'SSSM').LINE                           ONL02 HEL01260
         AIF   ('&TYPE' EQ 'SLOG').LINE                           ONL02 HEL01270
         AIF   ('&TYPE' EQ 'EXEC').LINE                           ONL02 HEL01280
         AIF   ('&TYPE' EQ 'OPER').LINE                           ONL02 HEL01290
         AIF   ('&TYPE' EQ 'SYSP').LINE                           ONL02 HEL01300
         AIF   ('&TYPE'(1,4) EQ 'QAHX').LINE                      ONL02 HEL01310
         AIF   ('&TYPE' EQ 'PAGE').HTEPAGE                        ONL02 HEL01320
         AIF   ('&TYPE' EQ 'END').HTEEND                          ONL02 HEL01330
         MNOTE 8,'#H MACRO - INVALID "TYPE" OPERAND'              ONL02 HEL01340
         MEXIT ,                                                  ONL02 HEL01350
.LINE    ANOP  ,                   DEFINE HELP MESSAGE LINE       ONL02 HEL01360
         AIF   (T'&OFFSET EQ 'O').SKIPOFF                         ONL02 HEL01370
         AIF   (T'&OFFSET NE 'N').OFFBAD                          ONL02 HEL01380
&OFF     SETA  &OFFSET                                            ONL02 HEL01390
.SKIPOFF ANOP ,                                                   ONL02 HEL01400
         AIF   (&OFF LE 79).OFFOK                                 ONL02 HEL01410
.OFFBAD  ANOP  ,                                                  ONL02 HEL01420
         MNOTE 8,'#H MACRO - INVALID "OFFSET" OPERAND'            ONL02 HEL01430
         MEXIT ,                                                  ONL02 HEL01440
.OFFOK   ANOP  ,                                                  ONL02 HEL01450
         AIF   (K'&TEXT LE 2).BADTEXT                             ONL02 HEL01460
         AIF   ('&TEXT'(1,1) NE '''').BADTEXT                     ONL02 HEL01470
         AIF   ('&TEXT'(K'&TEXT,1) NE '''').BADTEXT               ONL02 HEL01480
&L       SETA  K'&TEXT-2                                          ONL02 HEL01490
&N       SETA  1                                                  ONL02 HEL01500
.CNTLOOP ANOP  ,                                                  ONL02 HEL01510
&N       SETA  &N+1+&P                                            ONL02 HEL01520
         AIF   (&N GE K'&TEXT).GOTLEN                             ONL02 HEL01530
&P       SETB  ('&TEXT'(&N,2) EQ '''' OR '&TEXT'(&N,2) EQ '&&')   ONL02 HEL01540
&L       SETA  &L-&P                                              ONL02 HEL01550
         AGO   .CNTLOOP                                           ONL02 HEL01560
.BADTEXT ANOP  ,                                                  ONL02 HEL01570
         MNOTE 8,'#H MACRO - INVALID "MSG-TEXT" OPERAND'          ONL02 HEL01580
         MEXIT ,                                                  ONL02 HEL01590
.GOTLEN  ANOP  ,                                                  ONL02 HEL01600
&TLEN    SETA  &OFF+&L                                            ONL02 HEL01610
         AIF   (&TLEN LE 80).TLENOK                               ONL02 HEL01620
         MNOTE 8,'#H MACRO - "OFFSET" + "MSG-TEXT" IS TOO LONG'   ONL02 HEL01630
         MEXIT ,                                                  ONL02 HEL01640
.TLENOK  ANOP  ,                                                  ONL02 HEL01650
&MSGCNT  SETA  &MSGCNT+1                                          ONL02 HEL01660
         AIF   ('&TYPE' NE 'NORM').USETYPE                        ONL02 HEL01670
&NAME    DC    AL1(0,&L,&OFF,&USER),CL&L&TEXT                     ONL02 HEL01680
         MEXIT ,                                                  ONL02 HEL01690
.USETYPE ANOP  ,                                                  ONL02 HEL01700
         AIF   ('&TYPE'(1,4) EQ 'QAHX').TYPEWHO                   ONL02 HEL01710
&NAME    DC    AL1(QAHX&TYPE,&L,&OFF,&USER),CL&L&TEXT             ONL02 HEL01720
         MEXIT ,                                                  ONL02 HEL01730
.TYPEWHO ANOP  ,                                                  ONL02 HEL01740
&NAME    DC    AL1(&TYPE,&L,&OFF,&USER),CL&L&TEXT                 ONL02 HEL01750
         MEXIT ,                                                  ONL02 HEL01760
.HTEPAGE ANOP  ,                   END OF PAGE ENTRY              ONL02 HEL01770
         MNOTE *,'    &MSGCNT MESSAGES DEFINED FOR THIS SCREEN'   ONL02 HEL01780
&MSGCNT  SETA  0                                                  ONL02 HEL01790
&NAME    DC    XL3'00FFFF'         END OF PAGE                    ONL02 HEL01800
         MEXIT ,                                                  ONL02 HEL01810
&MSGCNT  SETA  0                                                  ONL02 HEL01820
.HTEEND  ANOP  ,                   END OF TABLE ENTRY             ONL02 HEL01830
         AIF   (&MSGCNT EQ 0).DONE                                ONL02 HEL01840
         MNOTE *,'    &MSGCNT MESSAGES DEFINED FOR THIS SCREEN'   ONL02 HEL01850
&MSGCNT  SETA  0                                                  ONL02 HEL01860
.DONE    ANOP  ,                                                  ONL02 HEL01870
&NAME    DC    XL3'FFFFFF'         END OF HELP DISPLAY            ONL02 HEL01880
         MEND  ,                                                  ONL02 HEL01890
         EJECT ,                                                  ONL02 HEL01900
******************************************************************ONL02 HEL01910
*                                                                 ONL02 HEL01920
*  DO ENTRY POINT HOUSEKEEPING                                    ONL02 HEL01930
*                                                                 ONL02 HEL01940
******************************************************************ONL02 HEL01950
         SPACE 1                                                  ONL02 HEL01960
HELP     QSTART ,                  START HELP CSECT ASSEMBLY      ONL02 HEL01970
         SPACE 1                                                  ONL02 HEL01980
         GBLB  &QPFK               PF-KEY OPTION                  ONL02 HEL01990
         SPACE 1                                                  ONL02 HEL02000
         USING WORK,R13            NOTE SAVE/WORK ADDRESSABILITY  ONL02 HEL02010
         SPACE 1                                                  ONL02 HEL02020
         L     R10,QVDSPL          ADDR OF DISPLAY WORK AREA      ONL02 HEL02030
         USING QDISPLAY,R10        NOTE DISPLAY ADDRESSABILITY    ONL02 HEL02040
         EJECT ,                                                  ONL02 HEL02050
******************************************************************ONL02 HEL02060
*                                                                 ONL02 HEL02070
*  BUILD HELP DISPLAY FROM HELP TABLE ENTRIES                     ONL02 HEL02080
*                                                                 ONL02 HEL02090
******************************************************************ONL02 HEL02100
         SPACE 1                                                  ONL02 HEL02110
         ZAP   DWORD,QDLNES        GET NUMBER OF AVAILABLE MESSAGEONL02 HEL02120
         CVB   R4,DWORD              LINES PER SCREEN IN BINARY   ONL02 HEL02130
         SLR   R5,R5               ZERO CURRENT LINE NUMBER       ONL02 HEL02140
         MVC   QDHLINE,HEADINGQ    SET HELP DISPLAY HEADING       WGH   HEL02150
         LA    R6,HELPQUE          ADDR OF QUEUE HELP TABLE ENTRY WGH   HEL02160
         CLI   QPARM1,C'Q'         HELP QUEUE?                    WGH   HEL02170
         BE    NEXTHTE             YES... USE INTRO HELP          WGH   HEL02180
         MVC   QDHLINE,HEADING     SET HELP DISPLAY HEADING       ONL02 HEL02190
         LA    R6,HELPTABL         ADDR OF FIRST HELP TABLE ENTRY ONL02 HEL02200
         USING HTEDSECT,R6         NOTE HTE ADDRESSABILITY        ONL02 HEL02210
         SPACE 2                                                  ONL02 HEL02220
*                                                                 ONL02 HEL02230
*  PROCESS NEXT HELP TABLE ENTRY                                  ONL02 HEL02240
*                                                                 ONL02 HEL02250
         SPACE 1                                                  ONL02 HEL02260
NEXTHTE  DS    0H                  PROCESS NEXT HELP TABLE ENTRY  ONL02 HEL02270
         CLC   HTEPAGE,=X'00FFFF'  IS THIS AN END OF PAGE ENTRY?  ONL02 HEL02280
         BE    DONEPAGE            SKIP IF YES                    ONL02 HEL02290
         CLC   HTEEND,=X'FFFFFF'   IS THIS AN END OF TABLE ENTRY? ONL02 HEL02300
         BE    DONETABL            SKIP IF YES                    ONL02 HEL02310
         SPACE 1                                                  ONL02 HEL02320
         ICM   R1,1,HTEFLAGS       GET MSG AUTHORITY REQUIREMENT  ONL02 HEL02330
         BZ    AUTHOK              SKIP IF MSG IS NOT RESTRICTED  ONL02 HEL02340
         EX    R1,AUTHTM           DOES USER HAVE AUTHORITY?      ONL02 HEL02350
         BO    AUTHOK              SKIP IF YES                    ONL02 HEL02360
         SPACE 1                                                  ONL02 HEL02370
*  INSERT CODE HERE IF YOU WISH TO OVERRIDE MESSAGE SUPPRESSION   ONL02 HEL02380
         SPACE 1                                                  ONL02 HEL02390
         B     MSGDONE             SKIP OVER MSG DISPLAY CODE     ONL02 HEL02400
         SPACE 1                                                  ONL02 HEL02410
AUTHTM   TM    QAHELP,*-*          EXECUTED - IS MSG AUTHORIZED?  ONL02 HEL02420
         SPACE 1                                                  ONL02 HEL02430
AUTHOK   DS    0H                  MSG IS AUTHORIZED FOR DISPLAY  ONL02 HEL02440
         SPACE 1                                                  ONL02 HEL02450
*  INSERT CODE HERE IF YOU WISH TO SUPPRESS AN AUTHORIZED MESSAGE ONL02 HEL02460
         SPACE 1                                                  ONL02 HEL02470
         MVC   QDMSG,QBLANK        BLANK OUT MSG WORK AREA        ONL02 HEL02480
         LA    R3,QDMSG            ADDR OF MSG WORK AREA          ONL02 HEL02490
         ST    R3,QDMSGA           SET MSG ADDR FOR DISPLAY RTN   ONL02 HEL02500
         SLR   R1,R1                                              ONL02 HEL02510
         IC    R1,HTEOFF           MSG OFFSET FROM COLUMN 1       ONL02 HEL02520
         LA    R3,0(R1,R3)         ADJUST AREA POINTER PER OFFSET ONL02 HEL02530
         SLR   R2,R2                                              ONL02 HEL02540
         ICM   R2,1,HTETLEN        LENGTH OF MSG TEXT             ONL02 HEL02550
         BZ    MSGLNERR            ERROR IF NO TEXT               ONL02 HEL02560
         LA    R1,0(R2,R1)         LENGTH OF OFFSET + TEXT        ONL02 HEL02570
         CL    R1,=F'80'           IS TOTAL LENGTH OK?            ONL02 HEL02580
         BH    MSGLNERR            ERROR IF TOO LONG              ONL02 HEL02590
         STH   R1,QDMLNG           SET MSG LENGTH FOR DISPLAY RTN ONL02 HEL02600
         BCTR  R2,0                TEXT LENGTH LESS 1 FOR EXECUTE ONL02 HEL02610
         EX    R2,MSGMVC           PLUG TEXT INTO MSG LINE        ONL02 HEL02620
         L     R15,=V(DISPLAY)     ADDR OF DISPLAY ROUTINE        ONL02 HEL02630
         BALR  R14,R15             LINK TO ROUTINE                ONL02 HEL02640
         SPACE 1                                                  ONL02 HEL02650
         LA    R5,1(,R5)           INCREMENT LINE COUNT           ONL02 HEL02660
         CLR   R5,R4               HAVE WE FILLED A SCREEN?       ONL02 HEL02670
         BL    MSGDONE             SKIP IF NO                     ONL02 HEL02680
         SLR   R5,R5               CLEAR LINE COUNT FOR NEW SCREENONL02 HEL02690
         SPACE 1                                                  ONL02 HEL02700
MSGDONE  DS    0H                  FINISHED WITH CURRENT MSG      ONL02 HEL02710
         SLR   R1,R1                                              ONL02 HEL02720
         IC    R1,HTETLEN          LENGTH OF MSG TEXT             ONL02 HEL02730
         LA    R6,HTETEXT(R1)      INDEX TO NEXT HELP TABLE ENTRY ONL02 HEL02740
         B     NEXTHTE             LOOP TO PROCESS NEXT ENTRY     ONL02 HEL02750
         SPACE 2                                                  ONL02 HEL02760
MSGMVC   MVC   0(*-*,R3),HTETEXT   EXECUTED - SET MSG TEXT        ONL02 HEL02770
         SPACE 1                                                  ONL02 HEL02780
MSGLNERR DS    0H                  MSG LENGTH ERROR               ONL02 HEL02790
         QTILT '*** QUEUE LOGIC ERROR - BAD HELP MSG LENGTH ***'  ONL02 HEL02800
         SPACE 2                                                  ONL02 HEL02810
*                                                                 ONL02 HEL02820
*  END OF PAGE ENTRY FOUND, FILL OUT CURRENT HELP SCREEN          ONL02 HEL02830
*                                                                 ONL02 HEL02840
         SPACE 1                                                  ONL02 HEL02850
DONEPAGE DS    0H                  COMPLETE CURRENT SCREEN        ONL02 HEL02860
         LA    R6,L'HTEPAGE(,R6)   POINT TO NEXT TABLE ENTRY      ONL02 HEL02870
         LTR   R5,R5               WAS ANYTHING DISPLAYED?        ONL02 HEL02880
         BNP   NEWPAGE             SKIP IF NO                     ONL02 HEL02890
         SPACE 1                                                  ONL02 HEL02900
         LR    R2,R4               COPY NUMBER OF AVAILABLE LINES ONL02 HEL02910
         SR    R2,R5               COMPUTE NUMBER OF UNUSED LINES ONL02 HEL02920
         BNP   NEWPAGE             SKIP IF SCREEN IS FULL         ONL02 HEL02930
         SPACE 1                                                  ONL02 HEL02940
         LA    R3,QDMSG            ADDR OF MSG WORK AREA          ONL02 HEL02950
         ST    R3,QDMSGA           SET MSG ADDR FOR DISPLAY RTN   ONL02 HEL02960
         MVI   QDMSG,C' '          SET A BLANK MESSAGE            ONL02 HEL02970
         MVC   QDMLNG,=H'1'         WITH A LENGTH OF ONE          ONL02 HEL02980
         SPACE 1                                                  ONL02 HEL02990
BLNKLOOP DS    0H                  FILL OUT CURRENT SCREEN        ONL02 HEL03000
         L     R15,=V(DISPLAY)     ADDR OF DISPLAY ROUTINE        ONL02 HEL03010
         BALR  R14,R15             LINK TO ROUTINE                ONL02 HEL03020
         BCT   R2,BLNKLOOP         LOOP IF MORE LINES TO BLANK    ONL02 HEL03030
         SPACE 1                                                  ONL02 HEL03040
NEWPAGE  DS    0H                  SETUP FOR NEXT SCREEN          ONL02 HEL03050
         SLR   R5,R5               CLEAR LINE COUNT FOR NEXT PAGE ONL02 HEL03060
         B     NEXTHTE             LOOP TO PROCESS NEXT HELP ENTRYONL02 HEL03070
         SPACE 2                                                  ONL02 HEL03080
*                                                                 ONL02 HEL03090
*  END OF HELP TABLE ENTRY FOUND, DONE WITH HELP DISPLAY          ONL02 HEL03100
*                                                                 ONL02 HEL03110
         SPACE 1                                                  ONL02 HEL03120
DONETABL DS    0H                  DONE WITH HELP TABLE           ONL02 HEL03130
         QSTOP ,                   RETURN TO DISPLAY LAST SCREEN  ONL02 HEL03140
         SPACE 2                                                  ONL02 HEL03150
******************************************************************ONL02 HEL03160
*                                                                 ONL02 HEL03170
*  READ-ONLY DATA                                                 ONL02 HEL03180
*                                                                 ONL02 HEL03190
******************************************************************ONL02 HEL03200
         SPACE 1                                                  ONL02 HEL03210
HEADING  DC    CL80'THE FOLLOWING SUBCOMMANDS ARE SUPPORTED:'           HEL03220
HEADINGQ DC    CL80' '                                                  HEL03220
         SPACE 1                                                  ONL02 HEL03240
         LTORG ,                   DEFINE LITERAL ORIGIN          ONL02 HEL03250
         TITLE 'DEFINE HELP SCREENS'                              ONL02 HEL03260
******************************************************************ONL02 HEL03270
*                                                                 ONL02 HEL03280
*  DEFINE HELP SCREENS                                            ONL02 HEL03290
*                                                                 ONL02 HEL03300
******************************************************************ONL02 HEL03310
         SPACE 1                                                  ONL02 HEL03320
         PUSH  PRINT               SAVE CURRENT PRINT STATUS      ONL02 HEL03330
         PRINT NOGEN               DON'T PRINT #H MACRO EXPANSIONSONL02 HEL03340
         SPACE 1                                                  ONL02 HEL03350
HELPQUE  DS    0C                  START OF HELP QUEUE            ONL02 HEL03360
         SPACE 1                                                  ONL02 HEL03370
 #H NORM,08,'THE QUEUE COMMAND ALLOWS A TSO USER TO DISPLAY JOBS'       HEL03380
 #H NORM,08,'AND VIEW SYSOUT DATASETS IN THE MVS JES2 QUEUE.'           HEL03390
 #H NORM,08,' '                                                         HEL03400
 #H NORM,08,'QUEUE PROVIDES FULL SCREEN VIEWING WITH FIND, UP, DOWN'    HEL03470
 #H NORM,08,'AND COLUMN SCROLL COMMANDS OR WITH PREDEFINED PF KEYS.'    HEL03480
 #H NORM,08,' '                                                         HEL03490
 #H NORM,08,'OUTPUT CAN BE SAVED ON DISK, COPIES CAN BE SPOOLED FOR'    HEL03500
 #H NORM,08,'PRINT, OUTPUT CAN BE DELETED, AND HELD OUTPUT RELEASED.'   HEL03510
 #H NORM,08,'JOBS MAY BE CANCELLED AND OPTIONALY PURGED.'               HEL03520
 #H NORM,08,' '                                                         HEL03530
 #H NORM,08,'THE + SIGN ON THE REPLY LINE INDICATES THAT MORE DATA'     HEL03540
 #H NORM,08,'IS AVAILABLE BY PRESSING ENTER.'                           HEL03550
 #H NORM,08,' '                                                         HEL03560
 #H NORM,08,'PRESS THE ENTER KEY TO VIEW A LIST OF VALID SUBCOMMANDS.'  HEL03570
         SPACE 1                                                  ONL02 HEL03580
 #H PAGE                           INDICATE END OF TABLE          ONL02 HEL03590
         EJECT ,                                                  ONL02 HEL03600
HELPTABL DS    0C                  START OF HELP TABLE            ONL02 HEL03610
         SPACE 1                                                  ONL02 HEL03620
 #H NORM,4,'END               - EXIT QUEUE COMMAND'                     HEL03630
 #H NORM,4,'STAT LEVEL        - DISPLAY ALL JOBS BEGINNING WITH LEVEL'  HEL03640
 #H NORM,4,'DJ   JOB NAME     - DISPLAY JOB BY FULL JOB NAME ONLY'      HEL03650
 #H NORM,4,'JH   JOBNAME      - DISPLAY JOB HISTORY'                    HEL03660
 #H NORM,4,'JCL  JOBNAME      - LIST JCL FOR A JOB'                     HEL03670
 #H NORM,4,'JLOG JOBNAME      - LIST JOBLOG FOR A JOB'                  HEL03680
 #H NORM,4,'JMSG JOBNAME      - LIST SYSTEM MESSAGES FOR A JOB'         HEL03690
 #H NORM,4,'DD   JOBNAME      - LIST SYSIN/SYSOUT DATASETS FOR A JOB'   HEL03700
 #H NORM,4,'PDDB JOBNAME      - LIST PDDB''S FOR A JOB'                 HEL03710
 #H NORM,4,'LIST JOBNAME DSID - LIST A SYSIN OR SYSOUT DATASET'         HEL03720
 #H NORM,4,'LIST * +/-N         LIST NEXT OR PREVIOUS N''TH DATASET'    HEL03730
 #H NORM,4,'SAVE NAME <TYPE>  - CREATE A COPY OF THE CURRENT DATASET'   HEL03740
 #H NORM,4,'          <#LINES>  CREATES ''USERID.NAME.SUFFIX'' '        HEL03740
 #H NORM,4,'SPIN CLASS <#LINES><DEST>- SPOOL COPY OF CURRENT DATASET'   HEL03760
 #H NORM,4,'PRINT ON CLASS DEST - OPEN SCREEN LOG'                      HEL03770
 #H NORM,4,'PRINT             - PRINT CURRENT SCREEN'                   HEL03780
 #H NORM,4,'PRINT OFF         - CLOSE SCREEN LOG'                       HEL03790
 #H NORM,4,' '                                                          HEL03800
 #H NORM,8,'DEFAULT FOR LEVEL IS LOGON ID'                              HEL03810
 #H NORM,8,'JOBNAME CAN BE JOB NAME, NUMBER, OR "*/=/ " FOR CURRENT JOBXHEL03820
               '                                                        HEL03830
 #H NORM,8,'DSID CAN BE DETERMINED BY USING SUBCOMMAND DD OR PDDB'      HEL03840
*#H NORM,8,'COUNT IS # OF LINES STARTING AT CURRENT LINE POINTER'       HEL03840
*#H NORM,8,'DSNAME WILL BE EXPANDED TO USERID.DSNAME.DATA'              HEL03850
         SPACE 1                                                  ONL02 HEL03860
 #H PAGE                           INDICATE END OF PAGE           ONL02 HEL03870
         EJECT ,                                                  ONL02 HEL03880
 #H NORM,4,'HELP              - PRODUCE THIS LIST OF SUBCOMMANDS'       HEL03890
 #H NORM,4,'HELP QUEUE        - BRIEF OVERVIEW OF QUEUE'                HEL03900
 #H NORM,4,'STACK             - PROCESS CLIST SUB-COMMANDS (DEFAULT)'   HEL03900
 #H NORM,4,'TERMINAL/ENDSTACK - DO NOT PROCESS CLIST SUB-COMMANDS'      HEL03900
 #H NORM,4,'QUIET/TALK        - USEFULL FOR CLIST SUB-COMMANDS'         HEL03900
 #H NORM,4,'MODEL #           - SET 3270 MODEL 2, 3, 4, OR 5'           HEL03910
         AIF  (NOT &QPFK).PFK1                                          HEL03920
 #H NORM,4,'PFK               - LIST PROGRAM FUNCTION KEYS'             HEL03930
 #H NORM,4,'PFK  NN  TEXT     - REDEFINE PROGRAM FUNCTION KEYS'         HEL03940
.PFK1    ANOP ,                                                         HEL03950
 #H NORM,4,'FIND ''STRING'' COL(SS,EE)  - FIND CHARACTER STRING'        HEL03960
 #H NORM,4,'FALL ''STRING'' COL(SS,EE)  - FIND ALL OCCURRENCES OF'      HEL03970
 #H NORM,4,'                              STRING (COL DEFAULTS TO ALL)' HEL03980
 #H NORM,4,'COL  #       - REPOSITION HORIZONTALLY TO COLUMN NUMBER'    HEL03990
 #H NORM,4,'@/MD #       - REPOSITION TO SPECIFIC RECORD NUMBER'        HEL04000
 #H NORM,4,'+/D  #       - REPOSITION FORWARD IN DATASET # RECORDS'     HEL04010
 #H NORM,4,'-/UP #       - REPOSITION BACKWARD IN DATASET # RECORDS'    HEL04020
 #H NORM,4,'T/TOP        - REPOSITION TO TOP OF DATASET'                HEL04030
 #H NORM,4,'B/BOTTOM     - REPOSITION TO BOTTOM OF DATASET'             HEL04040
 #H NORM,4,'HF/HB #      - FORWARD/BACKWARD # HALF PAGES'               HEL04050
 #H NORM,4,'PF/PB #      - FORWARD/BACKWARD # PAGES'                    HEL04060
 #H NORM,0,'NOTE: (1) STRING SPECIFICATION FOR FIND MAY BE DELIMITED'   HEL04070
 #H NORM,8,'  WITH BLANKS, SINGLE QUOTES, OR DOUBLE QUOTES'             HEL04080
         SPACE 1                                                  ONL02 HEL04090
 #H PAGE                           INDICATE END OF PAGE           ONL02 HEL04100
         EJECT ,                                                  ONL02 HEL04110
 #H ACTV,4,'DA           - DISPLAY ALL JOBS IN EXECUTION'               HEL04120
 #H ACTV,4,'DT           - DISPLAY TSO USERS'                           HEL04130
 #H ACTV,4,'DS           - DISPLAY SYSTEM TASKS'                        HEL04140
 #H ACTV,4,'DC TYPE      - DISPLAY CPU BATCH/STC/TSO'                   HEL04150
 #H BLOG,4,'DQ           - DISPLAY INPUT QUEUES'                        HEL04160
 #H BLOG,4,'DI CLASS     - DISPLAY ALL JOBS IN INPUT CLASS'             HEL04170
 #H BLOG,4,'AI CLASS     - DISPLAY JOBS AVAILABLE FOR PROCESSING'       HEL04180
 #H BLOG,4,'HI CLASS     - DISPLAY HELD JOBS IN INPUT CLASS'            HEL04190
 #H BLOG,4,'DF           - DISPLAY OUTPUT QUEUES'                       HEL04200
 #H BLOG,4,'DO CLASS     - DISPLAY ALL JOBS IN OUTPUT CLASS'            HEL04210
 #H BLOG,4,'AO CLASS     - DISPLAY AVAILABLE OUTPUT'                    HEL04220
 #H BLOG,4,'HO CLASS     - DISPLAY HELD OUTPUT'                         HEL04230
 #H BLOG,4,'WO           - DISPLAY ONLY OUTPUT JOBS WITH WRITER NAME SPEHEL04250
               CIFIED'                                                  HEL04260
 #H BLOG,4,' '                                                          HEL04270
 #H BLOG,8,'DEFAULT FOR CLASS IS ALL CLASSES'                           HEL04280
 #H SSSM,4,' '                                                          HEL04290
 #H SSSM,4,'CAN JOBNAME <P>        - CANCEL JOB ("P" OPTION FOR PURGE)' HEL04300
 #H SSSM,4,'DEL JOBNAME            - DELETE JOB'                        HEL04310
 #H SSSM,4,'REQ JOBNAME <NEWCLASS> - RELEASE JOB''S HELD OUTPUT'        HEL04320
         SPACE 1                                                  ONL02 HEL04330
 #H PAGE                           INDICATE END OF PAGE           ONL02 HEL04340
         EJECT ,                                                  ONL02 HEL04350
 #H SLOG,4,'SLOG  ID  <SEQ>   - LIST THE SYSTEM LOG DATASET'            HEL04360
 #H SLOG,4,'FTIME HH:MM:SS    - REPOSITION SYSTEM LOG TO GIVEN TIME'    HEL04370
 #H SLOG,8,'ID MAY BE A JOB# DETERMINED BY STATUS SYSLOG,'              HEL04380
 #H SLOG,8,'  OR IT MAY SPECIFY THE SYSTEM ID OF THE DESIRED SYSTEM'    HEL04390
 #H SLOG,8,'  (A NULL FIELD WILL LIST THE ACTIVE SYSTEM''S LOG)'        HEL04400
 #H SLOG,8,'DEFAULT FOR SEQ IS 0 (THE CURRENT SYSLOG DATASET)'          HEL04410
 #H SLOG,8,'  (USE A VALUE OF 1, 2, ... TO OBTAIN PREVIOUS DATASETS)'   HEL04420
 #H EXEC,4,' '                                                          HEL04430
 #H EXEC,4,'TSO CMD PRMS      - ISSUE ANY TSO COMMAND W/OPTIONAL PARMS' HEL04440
 #H SYSP,4,'PFX LEVEL         - CHANGE DEFAULT LEVEL FOR STATUS'        HEL04460
 #H SYSP,4,'XB  MTTR          - DISPLAY BLOCK FROM SYS1.HASPACE'        HEL04460
 #H SYSP,4,'                      USE "*" TO RE-SHOW AND "+" TO CHAIN'  HEL04470
 #H SYSP,4,'XD  JOBNAME DSID  - UNRESTRICTED DISPLAY OF DATASETS'       HEL04480
 #H SYSP,4,'XI                - DISPLAY ACTIVE INITIATORS'              HEL04490
 #H SYSP,4,'XJ  JOBNAME       - DISPLAY UNINTERPRETED JQES AND JOES'    HEL04500
 #H SYSP,4,'JQE JOBNAME       - DISPLAY JQE IN HEX/EBCDIC'              HEL04510
 #H SYSP,4,'IOT JOBNAME       - DISPLAY IOT IN HEX/EBCDIC'              HEL04520
 #H SYSP,4,'JCT JOBNAME <OFFSET>  - DISPLAY JCT IN HEX/EBCDIC'          HEL04530
 #H SYSP,4,'JOE JOBNAME       - DISPLAY JOES IN HEX/EBCDIC'             HEL04540
 #H SYSP,4,'HCT <BLOCK#><OFFSET>  - DISP CKPT AREAS (DAS,TGM,JIX,JQT,PSXHEL04550
               T,JOT)'
 #H SYSP,4,'TGPS              - LIST HIGH TRACK GROUP USERS (A VERY SLOXHEL04560
               W COMMAND)'                                              HEL04570
         SPACE 1                                                  ONL02 HEL04580
 #H PAGE                           INDICATE END OF PAGE           ONL02 HEL04590
         SPACE 1                                                  ONL02 HEL04600
 #H NORM,08,' '                                                         HEL04610
 #H NORM,08,'A TYPICAL SESSION MIGHT PROCEED AS FOLLOWS'                HEL04620
 #H NORM,08,' '                                                         HEL04630
 #H NORM,10,'STATUS           - PRODUCE LIST OF USERS JOBS'             HEL04640
 #H NORM,10,'JLOG NNNN        - DISPLAY JOB LOG FOR GIVEN JOB'          HEL04650
 #H NORM,10,'JMSG NNNN        - DISPLAY JOB MESSAGES FOR GIVEN JOB'     HEL04660
 #H NORM,10,'DD   NNNN        - DISPLAY LIST OF DATASETS FOR JOB'       HEL04670
 #H NORM,10,'PDDB NNNN        - USE PDDB IF DD TABLE DOES NOT EXIST'    HEL04680
 #H NORM,10,'LIST NNNN XXX    - DISPLAY SYSOUT DATASET BY DSID'         HEL04690
 #H NORM,10,'FIND STRING      - LOCATE CHARACTER STRING ON SCREEN'      HEL04700
 #H NORM,10,'PRINT ON         - CREATE COPY OF SCREEN IMAGE'            HEL04710
 #H NORM,10,'PF               - PAGE FORWARD IN DATASET'                HEL04720
 #H NORM,10,'PRINT            - CREATE COPY OF SCREEN IMAGE'            HEL04730
 #H NORM,10,'PRINT OFF        - SPOOL IMAGES TO PRINTER'                HEL04740
 #H NORM,10,'SPIN             - CREATE COPY OF DATASET ON PRINTER'      HEL04750
 #H NORM,10,'SAVE DSNAME      - CREATE COPY OF DATASET ON DISK'         HEL04760
 #H NORM,10,'DEL  NNNN        - DELETE OUTPUT FOR GIVEN JOB'            HEL04770
 #H NORM,10,'END              - TERMINATE QUEUE SESSION'                HEL04780
 #H NORM,10,' '                                                         HEL04790
 #H NORM,21,'NNNN  = JOB NUMBER FROM STATUS DISPLAY'                    HEL04800
 #H NORM,21,'XXX   = DATASET IDENTIFIER (DSID) FROM DD/PDDB DISPLAY'    HEL04810
         SPACE 1                                                  ONL02 HEL04820
 #H PAGE                           INDICATE END OF TABLE          ONL02 HEL04830
         SPACE 1                                                  ONL02 HEL04840
 #H END                            INDICATE END OF TABLE          ONL02 HEL04850
         SPACE 1                                                  ONL02 HEL04860
         POP   PRINT               RESTORE PRINT STATUS           ONL02 HEL04870
         TITLE 'DEFINE CONTROL BLOCK DSECT''S'                    ONL02 HEL04880
******************************************************************ONL02 HEL04890
*                                                                 ONL02 HEL04900
*  HELP TABLE ENTRY DSECT                                         ONL02 HEL04910
*                                                                 ONL02 HEL04920
******************************************************************ONL02 HEL04930
         SPACE 1                                                  ONL02 HEL04940
HTEDSECT DSECT ,                   HELP TABLE ENTRY DSECT         ONL02 HEL04950
HTE      DS    0X                  ALTERNATE DSECT NAME           ONL02 HEL04960
HTEFLAGS DS    AL1                 SUB-COMMAND AUTHORITY FLAGS    ONL02 HEL04970
HTETLEN  DS    AL1                 LENGTH OF MESSAGE TEXT         ONL02 HEL04980
HTEOFF   DS    AL1                 SCREEN OFFSET OF MESSAGE TEXT  ONL02 HEL04990
HTEUSER  DS    AL1                 USER FLAG BYTE                 ONL02 HEL05000
HTETEXT  DS    0C                  MESSAGE TEXT                   ONL02 HEL05010
         SPACE 1                                                  ONL02 HEL05020
         ORG   HTE                 REDEFINE DSECT                 ONL02 HEL05030
HTEPAGE  DC    0XL3'00FFFF'        END OF PAGE ENTRY              ONL02 HEL05040
HTEEND   DC    0XL3'00FFFF'        END OF TABLE ENTRY             ONL02 HEL05050
         SPACE 2                                                  ONL02 HEL05060
******************************************************************ONL02 HEL05070
*                                                                 ONL02 HEL05080
*  SAVE AREA/WORK AREA DSECT                                      ONL02 HEL05090
*                                                                 ONL02 HEL05100
******************************************************************ONL02 HEL05110
         SPACE 1                                                  ONL02 HEL05120
WORK     DSECT ,                   SAVE AREA/WORK AREA DSECT      ONL02 HEL05130
         DS    90F              5  OS SAVE AREAS                  ONL02 HEL05140
DWORD    DS    D                   SCRATCH DOUBLE WORD            ONL02 HEL05150
         SPACE 2                                                  ONL02 HEL05160
******************************************************************ONL02 HEL05170
*                                                                 ONL02 HEL05180
*  GENERATE QCOMMON DSECT                                         ONL02 HEL05190
*                                                                 ONL02 HEL05200
******************************************************************ONL02 HEL05210
         SPACE 1                                                  ONL02 HEL05220
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       ONL02 HEL05230
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL02 HEL05240
         $HASPEQU                                                       HEL05250
         QCOMMON                                                  ONL02 HEL05260
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL02 HEL05270
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    ONL02 HEL05280
         END   ,                                                  ONL02 HEL05290
