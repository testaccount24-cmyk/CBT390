CHCT     TITLE 'QUEUE COMMAND - DUMP A HCT IN HEX'                ONL01 CHC00010
CHCT     QSTART ,                  START CHCT CSECT ASSEMBLY      ONL01 CHC00020
*********************************************************************** CHC00030
* HCT                                                                 * CHC00040
         USING WORK,R13            BASE REG FOR LOCAL SAVE AREA   WGH   CHC00050
*********************************************************************** CHC00060
*                                                                     * CHC00070
*   CALL - READ JES2 CHECKPOINT ROUTINE                               * CHC00080
*                                                                     * CHC00090
*********************************************************************** CHC00100
         L     R15,=V(CKPT)        ADDR OF CKPT ROUTINE                 CHC00110
         BALR  R14,R15             GO TO IT                             CHC00120
*********************************************************************** CHC00130
*                                                                     * CHC00140
*   CALL HEXDUMP TO DUMP THE HCT CHECKPOINT AREA                      * CHC00150
*                                                                     * CHC00160
*********************************************************************** CHC00170
         L     R10,QVCKPT     BASE FOR CKPT WORK AREA                   CHC00180
         USING QCKPT,R10      ADDRESSING FOR IT                         CHC00190
         LH    R2,QLNG1       LENGTH OF 1ST PARM                   WGH  CHC00200
         LTR   R2,R2          ANYTHING THERE ?                     WGH  CHC00210
         BZ    JQTLR          NO... USE 1ST CHECKPOINT RECORD      WGH  CHC00220
         CLI   QPARM1,C'0'    IS FIRST POSITION A NUMBER ??        WGH  CHC00230
         BL    NAME           NO...  GET BLOCK NAME                WGH  CHC00240
         BCTR  R2,0           REDUCE LENGTH FOR EXECUTE            WGH  CHC00250
         EX    R2,PACKBLK     PACK INTO QDWORK                     WGH  CHC00260
         CVB   R1,QDWORK      PICK UP BLOCK NUMBER                 WGH  CHC00270
         LTR   R1,R1          CHECK IF ZERO                        WGH  CHC00280
         BP    *+8            NO... CONTINUE                       WGH  CHC00290
         LA    R1,1           YES.. FORCE TO 1                     WGH  CHC00300
*        C     R1,QCJOTL      COMPARE TO MAX                       WGH  CHC00310
*        BNH   *+8            OK... KEEP IT                        WGH  CHC00320
*        L     R1,QCJOTL      TOO BIG... USE MAX                   WGH  CHC00330
         BCTR  R1,0           REDUCE FOR ADDRESS CALCULATION       WGH  CHC00340
         MH    R1,=H'4096'    CALCULATE BUFFER OFFSET OF RECORD    WGH  CHC00350
         A     R1,QCJQTL      ADD TO BASE OF CKPT AREA             WGH  CHC00360
         B     NEXTPARM       PROCESS OFFSET                       WGH  CHC00370
NAME     LA    R1,CKPTABLE-6         GET BLOCK NAME                WGH  CHC00380
         LA    R15,(CKPTEND-CKPTABLE)/6        SIZE OF TABLE       WGH  CHC00390
TABLOOP  LA    R1,6(R1)       CHECK NEXT ENTRY                     WGH  CHC00400
         CLC   0(3,R1),QPARM1 IS THIS NAME CORRECT                 WGH  CHC00410
         BE    TABFIND        YES..  PROCESS NAME                  WGH  CHC00420
         BCT   R15,TABLOOP    NO...  TRY AGAIN                     WGH  CHC00430
         B     JQTLR          END OF TABLE - DEFAULT TO 1ST        WGH  CHC00440
TABFIND  EQU   *                                                   WGH  CHC00450
         MVC   LOADINW,LOADINST   MOVE IN MODEL INSTRUCTION        WGH  CHC00460
         MVC   LOADINW+2(2),4(R1) MOVE IN S TYPE ADDRESS           WGH  CHC00470
         LA    R2,0               SET OVERLAY TO 0 FOR EXECUTE     WGH  CHC00480
         EX    R2,LOADINW     ADDRESS OF CHECKPOINT RECORD TO R1   WGH  CHC00490
         B     NEXTPARM       GO PROCESS PARM2                     WGH  CHC00500
JQTLR    L     R1,QCJQTL      ADDRESS OF 1ST CHECKPOINT REC        WGH  CHC00510
NEXTPARM EQU   *              PROCESS PARM2                        WGH  CHC00520
         LR    R2,R1          SAVE ORIGINAL BASE                   WGH  CHC00530
         MVC   MSGCKPW(MSGCKPWL),MSGCKP   MOVE MSG TO WORK AREA    WGH  CHC00540
         S     R1,QCJQTL      CALCULATE CKPT RECORD OFFSET         WGH  CHC00550
         SR    R0,R0          CLEAR FOR DIVIDE                     WGH  CHC00560
         D     R0,=F'4096'    CALCULATE CKPT RECORD NUMBER         WGH  CHC00570
         MVC   MSGCKP3W,QBLANK          BLANK OUT OFFSET MSG       WGH  CHC00580
         LTR   R0,R0          IS THERE AN OFFSET                   WGH  CHC00590
         BZ    NOOFF          NO... CONTINUE                       WGH  CHC00600
         MVC   MSGCKP3W,MSGCKP3         YES.. MOVE IN OFFSET MSG   WGH  CHC00610
         MVC   MSGCKP3W+1(3),QPARM1     MOVE CONTROL BLOCK NAME    WGH  CHC00620
         ST    R0,QDWORK      STORE OFFSET                         WGH  CHC00630
         UNPK  MSGCKP4W(5),QDWORK+2(3)                             WGH  CHC00640
         TR    MSGCKP4W(4),TRTAB-C'0'                              WGH  CHC00650
         MVI   MSGCKP4W+4,C' ' CLEAR EXTRA BYTE                    WGH  CHC00660
NOOFF    LA    R1,1(R1)       MAKE BLOCK # RELEATIVE 1             WGH  CHC00670
         CVD   R1,QDWORK      CONVERT TO DECIMAL                   WGH  CHC00680
         ED    MSGCKP1W,QDWORK+6   INSERT NUMBER IN MSG            WGH  CHC00690
         L     R1,QCJOTL      GET TOTAL NUMBER OF CKPT RECORDS     WGH  CHC00700
         CVD   R1,QDWORK      CONVERT TO DECIMAL                   WGH  CHC00710
         ED    MSGCKP2W,QDWORK+6   INSERT NUMBER IN MSG            WGH  CHC00720
*        MVC   QDHLINE(MSGCKPWL),MSGCKPW  PUT MSG IN HEADER        WGH  CHC00730
         LA    R1,MSGCKPW     MESSAGE ADDRESS                      WGH  CHC00740
         ST    R1,QDMSGA      SAVE ADDRESS                         WGH  CHC00750
         LA    R1,MSGCKPWL    LENGTH                               WGH  CHC00760
         STH   R1,QDMLNG      SAVE FOR DISPLAY ROUTINE             WGH  CHC00770
         L     R15,=V(DISPLAY)                                     WGH  CHC00780
         BALR  R14,R15        DO IT                                WGH  CHC00790
         LR    R1,R2          RELOAD ORIGINAL BASE                 WGH  CHC00800
         LH    R15,QLNG2      LENGTH OF OFFSET PARM                WGH  CHC00810
         LTR   R15,R15        IS LENGTH ZERO ??                    WGH  CHC00820
         BNP   DOOFF          YES.. NO OFFSET                      WGH  CHC00830
         EX    R15,OFFTR2     NO... CONVERT TO HEX                 WGH  CHC00840
         EX    R15,OFFPACK    PACK INTO QDWORD                     WGH  CHC00850
         LH    R15,QDWORK     PICK UP OFFSET                       WGH  CHC00860
DOOFF    EQU   *                                                   WGH  CHC00870
         LR    R0,R15         SAVE OFFSET                          WGH  CHC00880
         AR    R1,R15         ADD OFFSET TO BASE                   WGH  CHC00890
         LH    R15,=H'4096'   LENGTH OF BUFFER                     WGH  CHC00900
         SR    R15,R0         LENGTH - OFFSET = LENGTH TO DUMP     WGH  CHC00910
         SLL   R0,16          MOVE OFFSET TO PROPER POSITION       WGH  CHC00920
         OR    R0,R15         INSERT INTO LENGTH REGISTER          WGH  CHC00930
*        L     R1,QCJQTL      ADDRESS OF HCT SAVEAREA                   CHC00940
*        LA    R0,$SAVEBEG-HCT  OFFSET TO START OF AREA                 CHC00950
*        LA    R0,0             OFFSET TO START OF AREA                 CHC00960
*        LA    R15,$SAVELEN   LENGTH OF $SAVEAREA                       CHC00970
*        LA    R15,4095       LENGTH TO DUMP                            CHC00980
*        SLL   R0,16          MOVE OFFSET TO PROPER POSITION            CHC00990
*        OR    R0,R15         INSERT INTO LENGTH REG                    CHC01000
         L     R15,=V(HEXDUMP) ADDRESS OF DUMP ROUTINE                  CHC01010
         BALR  R14,R15        LINK TO IT                                CHC01020
STOP     QSTOP                                                          CHC01030
*********************************************************************** CHC01040
*                                                                     * CHC01050
*   MISCELLANY                                                        * CHC01060
*                                                                     * CHC01070
*********************************************************************** CHC01080
LOADINST L     R1,*-*              MODEL INSTRUCTION              WGH   CHC01090
OFFTR1   TR    QPARM1,TABLEH-193   CONVERT TO HEX                 WGH   CHC01100
OFFTR2   TR    QPARM2,TABLEH-193   CONVERT TO HEX                 WGH   CHC01110
OFFPACK  PACK  QDWORK(3),QPARM2(1) PACK TO WORK AREA              WGH   CHC01120
PACKBLK  PACK  QDWORK,QPARM1(1)    PACK TO WORK AREA              WGH   CHC01130
TABLEH   DC    X'0A0B0C0D0E0F',CL41' ',C'0123456789',CL6' '       WGH   CHC01140
TRTAB    DC    C'0123456789ABCDEF'                                WGH   CHC01150
MSGCKP   DC    C'*** CHECKPOINT RECORD'                                 CHC01160
MSGCKP1  DC    X'40202020'                                              CHC01170
         DC    C' OF'                                                   CHC01180
MSGCKP2  DC    X'40202020',C' ***   '                                   CHC01190
MSGCKP3  DC    C' XXX BEGAN AT RECORD OFFSET '                          CHC01200
MSGCKP4  DC    CL5' '                                                   CHC01210
MSGCKPL  EQU   *-MSGCKP                                           WGH   CHC01220
         DS    0H                  FORCE TABLE ALIGNMENT          WGH   CHC01230
CKPTABLE DC    CL4'HCT',S(QCJQTL)                                 WGH   CHC01240
         DC    CL4'DAS',S(QCTDASA)                                WGH   CHC01250
         DC    CL4'TGM',S(QCTGMA)                                 WGH   CHC01260
         DC    CL4'JIX',S(QCJIXA)                                 WGH   CHC01270
         DC    CL4'JQT',S(QCJQTA)                                 WGH   CHC01280
         DC    CL4'PST',S(QCPSTA)                                 WGH   CHC01290
         DC    CL4'JOT',S(QCJOTA)                                 WGH   CHC01300
         DC    CL4'JCT',S(QCJCTA)                                 WGH   CHC01310
CKPTEND  EQU   *                                                        CHC01320
         LTORG                                                          CHC01330
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023 CHC01340
         QCOMMON                                                        CHC01350
CHCT     CSECT                                                          CHC01360
         DROP                                                           CHC01370
         COPY  $HASPGBL                                                 CHC01380
&MACSPLV SETC  '2'                                                      CHC01390
&SYSSPLV SETC  '2'                                                      CHC01400
&VERSION SETC  'SP 4.2.0'                                         WGH   CHC01410
&FMID    SETC  'HJE4420'                                          WGH   CHC01420
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01 CHC01430
        $HASPEQU                                                        CHC01440
        $LMT    ,                                                 WGH42 CHC01440
        $XECB   ,                                                 WGH42 CHC01440
PDS2BLEN EQU    1                                                 WGH42 CHC01440
         IFGRPL                                                         CHC01450
        $BUFFER                                                         CHC01460
        $JCT                                                            CHC01470
        $CAT                                                            CHC01480
        $JQE                                                            CHC01490
        $PCE                                                            CHC01500
        $PSV                                                            CHC01500
        $QSE                                                            CHC01500
        $MIT   ,                                                  ONL11 CHC01510
        $HCT                                                            CHC01520
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01 CHC01530
WORK     DSECT                                                     WGH  CHC01540
         DS     90F                 5 SAVE AREAS                   WGH  CHC01550
LOADINW  DS    F                    LOAD INSTRUCTION WORK AREA     WGH  CHC01560
MSGCKPW  DC    C'*** CHECKPOINT RECORD'                                 CHC01570
MSGCKP1W DC    X'40202020'                                              CHC01580
         DC    C' OF'                                                   CHC01590
MSGCKP2W DC    X'40202020',C' ***   '                                   CHC01600
MSGCKP3W DC    C' XXX BEGAN AT RECORD OFFSET '                          CHC01610
MSGCKP4W DC    CL5' '                                                   CHC01620
MSGCKPWL EQU   *-MSGCKPW                                                CHC01630
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023 CHC01640
         END                                                            CHC01650
