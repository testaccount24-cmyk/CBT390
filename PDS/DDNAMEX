DDNAME   TITLE 'QUEUE COMMAND - LIST DDNAMES AND DSIDS FOR A JOB' ONL01 DDN00010
DDNAME   QSTART ,                  START DDNAME CSECT ASSEMBLY    ONL01 DDN00020
*********************************************************************** DDN00030
* RNB CHANGES:                                                        * DDN00040
*      (1) RNB11 - ALLOW COMMAND OF FORM   DD NAME S  WHERE S MEANS   * DDN00050
*                  TO LIST THE SPIN DATA SETS EVEN IF A BATCH JOB.    * DDN00060
*                  ADDED BECAUSE OUR IMS SYSTEM SPINS OFF DUMP DATA   * DDN00070
*                  SETS THAT WE WANT TO LOOK AT.                      * DDN00080
*      (2) RNB12 - WITH SP2 JES2 WE ALWAYS SEEM TO GET THE 'ALREADY   * DDN00090
*                  PRINTED' MESSAGE FOR SPIN DATA SETS. THIS CHANGE   * DDN00100
*                  BYPASSES THE MESSAGE IF QSP=1.                     * DDN00110
*********************************************************************** DDN00120
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA         DDN00130
         L     R10,QVCKPT     LOAD BASE REG                             DDN00140
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA            DDN00150
         L     R9,QVDSPL      LOAD BASE REG                             DDN00160
         USING WORK,R13                                                 DDN00170
*********************************************************************** DDN00180
*                                                                     * DDN00190
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      * DDN00200
*                                                                     * DDN00210
*********************************************************************** DDN00220
         L     R15,=V(FINDJOB) ROUTINE TO FIND JOB                      DDN00230
         BALR  R14,R15        GO TO IT                                  DDN00240
         SPACE 2                                                  ONL02 DDN00250
******************************************************************ONL02 DDN00260
*                                                                 ONL02 DDN00270
*   PERFORM AUTHORIZATION CHECK FOR JOB                           ONL02 DDN00280
*                                                                 ONL02 DDN00290
******************************************************************ONL02 DDN00300
         SPACE 1                                                  ONL02 DDN00310
         QAUTHCK QADD              PERFORM JOB AUTHORIZATION      ONL02 DDN00320
         SPACE 2                                                  ONL02 DDN00330
*********************************************************************** DDN00340
*                                                                     * DDN00350
*   DETERMINE JOB TYPE (BATCH OR TSO)                                 * DDN00360
*                                                                     * DDN00370
*********************************************************************** DDN00380
         USING JCT,R1         BASE REG FOR JCT                          DDN00390
         USING PDB,R2         BASE REG FOR PDDB                         DDN00400
         USING IOT,R3         BASE REG FOR IOT                    WGH   DDN00410
         L     R1,QCJCTA      A(JCT)                                    DDN00420
         CLI   QPARM2,C'S'                                        RNB11 DDN00430
         BE    DDTSO                                              RNB11 DDN00440
         CLC   JCTJOBID(3),=CL3'TSU' ?/TSO USER                         DDN00450
         BE    DDTSO          YES. GO PROCESS                           DDN00460
         CLC   JCTJOBID(3),=CL3'STC' ?/STARTED TASK                     DDN00470
         BE    DDTSO          YES. GO PROCESS                           DDN00480
*********************************************************************** DDN00490
*                                                                     * DDN00500
*   LOCATE PDDB NUMBER 5                                              * DDN00510
*                                                                     * DDN00520
*********************************************************************** DDN00530
DDJOB    L     R3,QCIOTA      LOAD BASE REG                             DDN00540
         SH    R3,=AL2(IOTSTART-IOT)      HASP BUFFER PREFIX       WGH  DDN00550
         LR    R4,R3          BASE OF IOT                               DDN00560
         CLC   IOTID,=CL4'IOT'  WAS AN IOT READ FROM SPOOL ?      ONL03 DDN00570
         BNE   BADIOT         ERROR IF NO                         ONL03 DDN00580
         CLC   QPJOBID,IOTJBKEY  IS THE IOT'S JOB KEY VALID ?     ONL03 DDN00590
         BNE   BADIOT         ERROR IF NO                         ONL03 DDN00600
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB                   DDN00610
         LR    R2,R3          BASE OF IOT                               DDN00620
         A     R2,IOTPDDB     OFFSET TO FIRST PDDB IN IOT         WGH   DDN00630
         MVC   QPDSID,=H'0'   NULLIFY VALIDITY FOR LISTDS         WGH   DDN00640
FINDDS   CLC   =F'5',PDBDSKEY IS THIS THE DATASET?                WGH42 DDN00650
         BE    FOUNDDS        YES. CONTINUE.                            DDN00660
         LA    R2,PDBLENG(R2) NO. LOOK AT NEXT PDDB.                    DDN00670
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB?          DDN00680
         BL    FINDDS         NO. TRY AGAIN.                            DDN00690
         QTILT '*** JOB DOES NOT HAVE DD TABLE - TRY PDDB ***'          DDN00700
FOUNDDS  L     R4,PDBMTTR     DISK ADDR OF FIRST BLOCK                  DDN00710
         L     R5,QCBLKA      ADDR OF DATASET BLOCK IOAREA              DDN00720
         MVC   QDMSG,QBLANK   BLANK OUT THE MESSAGE AREA                DDN00730
         B     FIRST          PROCESS DATASET                           DDN00740
*********************************************************************** DDN00750
*                                                                     * DDN00760
*   PROCESS DATASET                                                   * DDN00770
*                                                                     * DDN00780
*********************************************************************** DDN00790
NEXTBLK  L     R4,HDBNXTRK-BUFSTART(R5)   ADDR OF NEXT BLOCK     WGH42  DDN00800
FIRST    LTR   R4,R4          IS THE DISK ADDR ZERO?                    DDN00810
         BZ    END            YES. END OF DATASET.                      DDN00820
         BAL   R8,READ        READ A BLOCK                              DDN00830
         CLC   QPJOBID,HDBJBKEY-BUFSTART(R5)  JOBID MATCH       WGH42   DDN00840
         BNE   END            NO. END OF DATASET.                       DDN00850
         CLC   =H'5',HDBDSKEY-BUFSTART+2(R5)  DSID MATCH        WGH42   DDN00860
         BNE   END            NO. END OF DATASET.                       DDN00870
         LA    R4,HDBSTART-BUFSTART(R5)   FIRST RECORD IN BLOCK  WGH42  DDN00880
*********************************************************************** DDN00890
*                                                                     * DDN00900
*   PROCESS RECORDS                                                   * DDN00910
*                                                                     * DDN00920
*********************************************************************** DDN00930
NEXTREC  CLI   0(R4),X'FF'    IS LENGTH BYTE FF?                        DDN00940
         BE    NEXTBLK        YES. END OF BLOCK.                        DDN00950
         TM    1(R4),X'10'    IS THIS A SPANNED RECORD?                 DDN00960
         BO    SPAN           YES. SKIP IT.                             DDN00970
         SR    R6,R6          ZERO OUT REG                              DDN00980
         IC    R6,0(R4)       INSERT LENGTH                             DDN00990
         TM    5(R4),2        IS THIS AN EXEC RECORD?                   DDN01000
         BO    EXEC           YES. PROCESS IT.                          DDN01010
         TM    5(R4),4        IS THIS A DD RECORD?                      DDN01020
         BO    DD             YES. PROCESS IT.                          DDN01030
SKIPREC  LA    R4,3(R6,R4)    INCREMENT TO NEXT RECORD                  DDN01040
         B     NEXTREC        PROCESS NEXT RECORD                       DDN01050
SPAN     LH    R6,2(R4)       LENGTH OF SEGMENT                         DDN01060
         TM    1(R4),X'08'    IS THIS THE FIRST SEGMENT?                DDN01070
         BO    SPANFRST       YES. USE HEADER LENGTH OF 6.              DDN01080
         LA    R4,4(R6,R4)    INCREMENT TO NEXT RECORD                  DDN01090
         B     NEXTREC        PROCESS NEXT RECORD                       DDN01100
SPANFRST LA    R4,6(R6,R4)    INCREMENT TO NEXT RECORD                  DDN01110
         B     NEXTREC        PROCESS NEXT RECORD                       DDN01120
END      QSTOP                                                          DDN01130
*********************************************************************** DDN01140
*                                                                     * DDN01150
*   PROCESS AN EXEC RECORD                                            * DDN01160
*                                                                     * DDN01170
*********************************************************************** DDN01180
EXEC     EQU   *                                                   WGH  DDN01190
         CLI   6(R4),X'08'    PROC EXEC ?                          WGH  DDN01200
         BE    PROC                                                WGH  DDN01210
         MVC   PROCSTEP,QBLANK BLANK OUT THE PROC STEPNAME         WGH  DDN01220
         MVC   STEPNAME,QBLANK BLANK OUT THE STEPNAME                   DDN01230
         CLI   7(R4),X'94'    IS THERE A STEPNAME?                      DDN01240
         BNE   SKIPREC        NO. SKIP THE REST.                        DDN01250
         SR    R1,R1          ZERO OUT R1                               DDN01260
         IC    R1,9(R4)       LENGTH OF STEPNAME                        DDN01270
         SH    R1,=H'1'       DECREMENT BY 1                            DDN01280
         BM    SKIPREC        STEPNAME WAS ZERO LENGTH.                 DDN01290
         EX    R1,MVCSTEP     MOVE THE STEPNAME                         DDN01300
         B     SKIPREC        CONTINUE PROCESSING                       DDN01310
PROC     EQU   *                                                   WGH  DDN01320
         MVC   PROCSTEP,QBLANK                                     WGH  DDN01330
         CLI   7(R4),X'94'    IS THERE A STEPNAME?                 WGH  DDN01340
         BNE   SKIPREC        NO. SKIP THE REST                    WGH  DDN01350
         SR    R1,R1                                               WGH  DDN01360
         IC    R1,9(R4)       LENGTH OF STEPNAME                   WGH  DDN01370
         SH    R1,=H'1'       REDUCE BY 1                          WGH  DDN01380
         BM    SKIPREC        STEPNAME WAS ZERO LENGTH             WGH  DDN01390
         EX    R1,MVCPROC     MOVE THE STEPNAME                    WGH  DDN01400
         B     SKIPREC        CONTINUE                             WGH  DDN01410
*********************************************************************** DDN01420
*                                                                     * DDN01430
*   PROCESS DD RECORDS                                                * DDN01440
*                                                                     * DDN01450
*********************************************************************** DDN01460
DD       TM    6(R4),X'30'    IS THIS A SYSIN OR SYSOUT DD?             DDN01470
         BZ    SKIPREC        NO. SKIP THE RECORD.                      DDN01480
         MVC   DDN,QBLANK     BLANK OUT THE DDNAME                      DDN01490
         MVC   DSID,QBLANK    BLANK OUT THE DSID                        DDN01500
         MVC   DSRECCT,QBLANK BLANK OUT THE DS RECORD COUNT             DDN01510
         MVC   DSCLASS,QBLANK BLANK OUT THE DS CLASS                    DDN01520
         LA    R7,7(R4)       ADDR OF FIRST KEY                         DDN01530
         LR    R8,R6          REMAINING LENGTH OF RECORD                DDN01540
         SR    R15,15         ZERO OUT R15                              DDN01550
         SR    R14,R14        ZERO OUT R14                              DDN01560
         SR    R1,R1          ZERO OUT R1                               DDN01570
TRYFLD   CLI   0(R7),X'6E'    IS THIS THE DDNAME?                       DDN01580
         BE    DDKEY          YES. PROCESS IT.                          DDN01590
         CLI   0(R7),X'4A'    IS THIS THE DSNAME?                       DDN01600
         BNE   NEXTFLD        NO. GET NEXT FIELD                        DDN01610
         CLC   3(3,R7),=C'JES' YES. IS THIS TRULY A JES2 SYSIN/OUT DS?  DDN01620
         BE    DSKEY          YES. PROCESS IT.                          DDN01630
NEXTFLD  IC    R1,1(R7)       NUMBER OF SUBFIELDS                       DDN01640
         LA    R7,2(R7)       UPDATE LOCATION                           DDN01650
         SH    R8,=H'2'       REMAINING COUNT                           DDN01660
         SR    R8,R1          REMAINING COUNT                           DDN01670
         BNP   SKIPREC        RECORD IS EXHAUSTED                       DDN01680
         LTR   R1,R1          ARE THERE ANY SUBFIELDS?                  DDN01690
         BZ    TRYFLD         NO. TRY NEXT FIELD.                       DDN01700
LOOPFLD  TM    0(R7),X'80'    IS THIS A SUB-SUB-FIELD                   DDN01710
         BZ    NOSUB          NO. CONTINUE.                             DDN01720
         NI    0(R7),X'7F'    CLEAR THE HEX 80 BIT                      DDN01730
         IC    R14,0(R7)      NUMBER OF SUB-SUB-FIELDS                  DDN01740
         LA    R7,1(R7)       UPDATE LOCATION                           DDN01750
         SH    R8,=H'1'       REMAINING COUNT                           DDN01760
         SR    R8,R14         REMAINING COUNT                           DDN01770
         BNP   SKIPREC        RECORD IS EXHAUSTED                       DDN01780
         AR    R1,R14         INCREASE NUMBER OF SUBFIELDS              DDN01790
         B     YESSUB         DECREMENT AND TRY AGAIN                   DDN01800
NOSUB    IC    R15,0(R7)      SUBFIELD LENGTH                           DDN01810
         LA    R7,1(R15,R7)   ADD TO LOCATION                           DDN01820
         SR    R8,R15         REMAINING COUNT                           DDN01830
         BNP   SKIPREC        RECORD IS EXHAUSTED                       DDN01840
YESSUB   BCT   R1,LOOPFLD     DO NEXT SUBFIELD                          DDN01850
         B     TRYFLD         TRY NEXT FIELD                            DDN01860
DDKEY    IC    R1,2(R7)       LENGTH OF DDNAME                          DDN01870
         LTR   R1,R1          IS THE LENGTH ZERO?                       DDN01880
         BZ    NEXTFLD        YES. SKIP THE FIELD.                      DDN01890
         BCTR  R1,0           DECREMENT BY 1                            DDN01900
         EX    R1,MVCDDN      MOVE THE DDNAME                           DDN01910
         B     NEXTFLD        PROCESS NEXT FIELD                        DDN01920
DSKEY    SR    R1,R1          CLEAR FOR INSERT                   WGH    DDN01930
         IC    R1,2(R7)       GET LENGTH OF DSNAME               WGH
         LA    R1,0(R1,R7)    POINT TO LAST 3 CHARS OF DSN       WGH
         MVC   DSID+1(3),0(R1)  MOVE THE DSID                    WGH    DDN01930
         L     R15,=V(FINDPDDB) ADDR OF FINDPDDB MODULE                 DDN01940
         BALR  R14,R15        GO TO IT                                  DDN01950
         MVC   QDHLINE,HEADING MOVE IN HEADING                          DDN01960
         LA    R1,QDMSG       ADDR OF MESSAGE LINE                      DDN01970
         ST    R1,QDMSGA      STORE IN MESSAGE ADDR                     DDN01980
         MVC   QDMLNG,=H'80'  MESSAGE LENGTH                            DDN01990
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE                   DDN02000
         BALR  R14,R15        GO TO IT                                  DDN02010
         B     SKIPREC        PROCESS NEXT RECORD                       DDN02020
*********************************************************************** DDN02030
*                                                                     * DDN02040
*   PROCESS DD TSU                                                    * DDN02050
*                                                                     * DDN02060
*********************************************************************** DDN02070
DDTSO    L     R5,QCBLKA      ADDR OF DATASET BLOCK IOAREA              DDN02080
         LR    R3,R5          BASE OF IOAREA                            DDN02090
         L     R4,JCTSPIOT    ADDR OF FIRST SPIN IOT                    DDN02100
TSO010   LTR   R4,R4          IS IOT ADDR ZERO?                         DDN02110
         BZ    DDJOB          YES, GO READ REGULAR IOT'S                DDN02120
         L     R3,QCBLKA      RELOAD POINTER TO IOAREA             WGH  DDN02130
         BAL   R8,READ        READ IOT                                  DDN02140
         SH    R3,=AL2(IOTSTART-IOT)      HASP BUFFER PREFIX       WGH  DDN02150
         LR    R4,R3          BASE OF IOT                               DDN02160
         CLC   IOTID,=CL4'IOT'  WAS AN IOT READ FROM SPOOL ?      ONL03 DDN02170
         BNE   BADIOT         ERROR IF NO                         ONL03 DDN02180
         CLC   QPJOBID,IOTJBKEY  IS THE IOT'S JOB KEY VALID ?     ONL03 DDN02190
         BNE   BADIOT         ERROR IF NO                         ONL03 DDN02200
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB                   DDN02210
         LR    R2,R3          BASE OF IOT                               DDN02220
         A     R2,IOTPDDB     OFFSET TO FIRST PDDB IN IOT         WGH   DDN02230
TSO020   CLI   PDBFLAG1,X'00' IS THIS PDDB VALID                        DDN02240
         BE    TSO040         NO, GET NEXT IOT                          DDN02250
         MVC   QDMSG,QBLANK   BLANK MESSAGE LINE                        DDN02260
         MVC   DDN,=CL8'SPIN-DS'      MOVE IN DDNAME                    DDN02270
         SR    R0,R0          CLEAR REG 0                               DDN02280
         L     R0,PDBDSKEY    CONVERT                             WGH42 DDN02290
         CVD   R0,CONVERT        DATA SET                               DDN02300
         MVC   DSID,ED4               ID  TO                            DDN02310
         ED    DSID,CONVERT+6             ZERO                          DDN02320
         L     R0,PDBRECCT    CONVERT                                   DDN02330
         CVD   R0,CONVERT        RECORD                                 DDN02340
         MVC   DSRECCT,ED8          COUNT TO                            DDN02350
         ED    DSRECCT,CONVERT+4        CHARACTER                       DDN02360
         MVC   DSCLASS,PDBCLASS  MOVE IN SYSOUT CLASS                   DDN02370
         B     TSO030            SKIP TEST                        WGH   DDN02380
*        TM    PDBFLAG1,PDB1PSO  HAS DATA SET BEEN PRINTED        WGH42 DDN02390
*        BO    TSO030         NO                                  WGH42 DDN02400
*        MVC   MESSAGE,PRTMSG INDICATE DATA SET PRINTED           WGH42 DDN02410
TSO030   MVC   QDHLINE,HEADING MOVE IN HEADING                          DDN02420
         LA    R1,QDMSG       ADDR OF MESSAGE LINE                      DDN02430
         ST    R1,QDMSGA      STORE IN MESSAGE ADDR                     DDN02440
         MVC   QDMLNG,=H'80'  MESSAGE LENGTH                            DDN02450
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE                   DDN02460
         BALR  R14,R15        GO TO IT                                  DDN02470
         LA    R2,PDBLENG(R2) LOOK AT NEXT PDDB                         DDN02480
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB           DDN02490
         BL    TSO020         NO, TRY AGAIN                             DDN02500
TSO040   L     R4,IOTIOTTR    DISK ADDR OF NEXT IOT                     DDN02510
         B     TSO010         GO SEARCH THE NEXT IOT                    DDN02520
*********************************************************************** DDN02530
*                                                                     * DDN02540
*   READ A BLOCK FROM HASPACE                                         * DDN02550
*                                                                     * DDN02560
*********************************************************************** DDN02570
READ     ST    R4,QCTRAK      STORE DISK ADDR                           DDN02580
         LR    R1,R5          IOAREA ADDRESS                            DDN02590
         L     R15,=V(READSPC) ADDR OF ROUTINE TO READ HASPACE          DDN02600
         BALR  R14,R15        GO TO IT                                  DDN02610
         BR    R8             RETURN TO CALLER                          DDN02620
         SPACE 3                                                  ONL03 DDN02630
BADIOT   EQU   *                                                        DDN02640
         QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       ONL03 DDN02650
         SPACE 2                                                  ONL03 DDN02660
*********************************************************************** DDN02670
*                                                                     * DDN02680
*   MISCELLANY                                                        * DDN02690
*                                                                     * DDN02700
*********************************************************************** DDN02710
         LTORG                                                          DDN02720
MVCSTEP  MVC   STEPNAME(1),10(R4)                                       DDN02730
MVCPROC  MVC   PROCSTEP(1),10(R4)                                 WGH   DDN02740
MVCDDN   MVC   DDN(1),3(R7)                                             DDN02750
HEADING  DC    CL12'STEPNAME  '                                   WGH   DDN02760
         DC    CL68'PROCSTEP    DDNAME      DSID      LINES   CLASS'    DDN02770
ED4      DC    X'40202120'                                              DDN02780
ED5      DC    X'4020202120'                                            DDN02790
ED8      DC    X'4020202020202120'                                      DDN02800
PRTMSG   DC    CL15'ALREADY PRINTED'                                    DDN02810
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC DDN02820
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023 DDN02830
DDNAME   CSECT ,                                                  UF023 DDN02840
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01 DDN02850
         $HASPEQU                                                       DDN02860
         IFGRPL                                                         DDN02870
         $BUFFER ,                                                      DDN02880
         $TAB                                                           DDN02890
         $JCT                                                           DDN02900
         $PDDB                                                          DDN02910
         $IOT                                                           DDN02920
         QCOMMON                                                        DDN02930
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01 DDN02940
         ORG   QDMSG                                                    DDN02950
STEPNAME DS    CL8                                                      DDN02960
         DS    CL4                                                      DDN02970
PROCSTEP DS    CL8                 SPACE FOR PROCSTEP NAME        WGH   DDN02980
         DS    CL4                 ALSO SEE MODULE FINDPDDB(Q25)  WGH   DDN02990
DDN      DS    CL8 END OF DATA. LAST REC #'                             DDN03000
         DS    CL4                                                      DDN03010
DSID     DS    CL4                                                      DDN03020
         DS    CL4                                                      DDN03030
DSRECCT  DS    CL8                                                      DDN03040
         DS    CL4                                                      DDN03050
DSCLASS  DS    CL1                                                      DDN03060
         DS    CL6                                                      DDN03070
MESSAGE  DS    CL15                                                     DDN03080
WORK     DSECT                                                          DDN03090
         DS    90F               5 SAVE AREAS                           DDN03100
CONVERT  DS    D                                                        DDN03110
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023 DDN03120
         END                                                            DDN03130
